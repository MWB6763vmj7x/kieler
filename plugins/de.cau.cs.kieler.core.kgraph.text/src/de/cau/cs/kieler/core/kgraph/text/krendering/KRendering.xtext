/*
 * KIELER - Kiel Integrated Environment for Layout Eclipse RichClient
 * 
 * http://www.informatik.uni-kiel.de/rtsys/kieler/
 * 
 * Copyright 2012 by
 * + Christian-Albrechts-University of Kiel
 *   + Department of Computer Science
 *     + Real-Time and Embedded Systems Group
 * 
 * This code is provided under the terms of the Eclipse Public License (EPL).
 * See the file epl-v10.html for the license text.
 */
grammar de.cau.cs.kieler.core.kgraph.text.krendering.KRendering with de.cau.cs.kieler.core.kgraph.text.klayoutdata.KLayoutData

import "platform:/resource/de.cau.cs.kieler.core.krendering/model/krendering.ecore" 

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

KRenderingLibrary returns KRenderingLibrary:
    {KRenderingLibrary}
    'RenderingLibrary'
    '{'
        (renderings+=KRendering ( ","? renderings+=KRendering)*)?
    '}';

KRendering returns KRendering:
    KEllipse | KRectangle | KRoundedRectangle | KPolyline_Impl | KPolygon | KImage | KArc | KRenderingRef | KChildArea | KText | KCustomRendering | KSpline | KRoundedBendsPolyline;

KPlacementData returns KPlacementData:
    KDecoratorPlacementData | KGridPlacementData | KDirectPlacementData;

KStyle returns KStyle:
    KForegroundColor | KBackgroundColor | KLineWidth | KAlpha | KLineStyle | KRotation | KFontBold | KFontItalic | KFontName | KFontSize | KVerticalAlignment | KHorizontalAlignment; //| KGradient;

KPlacement returns KPlacement:
    KGridPlacement;

KXPosition returns KXPosition:
    KLeftPosition | KRightPosition;

KYPosition returns KYPosition:
    KTopPosition | KBottomPosition;

KRenderingRef returns KRenderingRef:
    {KRenderingRef}
    'RenderingRef' rendering=[KRendering|EString]
    ('{'
        ('styles' ':'?  styles+=KStyle ( ","? styles+=KStyle)*)?
        ('placementData' ':'? placementData=KPlacementData)?
    '}')?;
    
KEllipse returns KEllipse:
    {KEllipse}
    'Ellipse'
    ('{'
        ('styles' ':'? styles+=KStyle ( ","? styles+=KStyle)*)?
        ('placementData' ':'? placementData=KPlacementData)?
        ('childPlacement' ':'? childPlacement=KPlacement)?
        ('children' ':'? children+=KRendering ( ","? children+=KRendering)*)?
    '}')?;

KRectangle returns KRectangle:
    {KRectangle}
    'Rectangle'
    ('{'
        ('styles' ':'? styles+=KStyle ( ","? styles+=KStyle)*)?
        ('placementData' ':'? placementData=KPlacementData)?
        ('childPlacement' ':'? childPlacement=KPlacement)?
        ('children' ':'? children+=KRendering ( ","? children+=KRendering)*)?
    '}')?;

KRoundedRectangle returns KRoundedRectangle:
    {KRoundedRectangle}
    'RoundedRectangle' cornerWidth=EFloat ','? cornerHeight=EFloat
    ('{'
        ('styles' ':'? styles+=KStyle ( ","? styles+=KStyle)*)?
        ('placementData' ':'? placementData=KPlacementData)?
        ('childPlacement' ':'? childPlacement=KPlacement)?
        ('children' ':'? children+=KRendering ( ","? children+=KRendering)*)?
    '}')?;

KPolyline_Impl returns KPolyline:
    {KPolyline}
    'Polyline'
    ('{'
        'points' ':'? points+=KPosition ( ","? points+=KPosition)* 
        ('styles' ':'? styles+=KStyle ( ","? styles+=KStyle)*)?
        ('placementData' ':'? placementData=KPlacementData)?
        ('childPlacement' ':'? childPlacement=KPlacement)?
        ('children' ':'? children+=KRendering ( ","? children+=KRendering)*)?
    '}')?;

KRoundedBendsPolyline returns KRoundedBendsPolyline:
    {KRoundedBendsPolyline}
    'RoundedBendsPolyline' bendRadius=EFloat
    ('{'
        ('styles' ':'? styles+=KStyle ( ","? styles+=KStyle)*)?
        ('placementData' ':'? placementData=KPlacementData)?
        ('childPlacement' ':'? childPlacement=KPlacement)?
        ('children' ':'? children+=KRendering ( ","? children+=KRendering)*)?
    '}')?;

KPolygon returns KPolygon:
    {KPolygon}
    'Polygon'
    ('{'
        ('styles' ':'? styles+=KStyle ( ","? styles+=KStyle)*)?
        ('placementData' ':'? placementData=KPlacementData)?
        ('childPlacement' ':'? childPlacement=KPlacement)?
        ('children' ':'? children+=KRendering ( ","? children+=KRendering)*)?
    '}')?;

KImage returns KImage:
    {KImage}
    'Image' ((bundleName=EString) | '-') imagePath=EString
    ('{'
        ('styles' ':'? styles+=KStyle ( ","? styles+=KStyle)*)?
        ('placementData' ':'? placementData=KPlacementData)?
        ('childPlacement' ':'? childPlacement=KPlacement)?
        ('children' ':'? children+=KRendering ( ","? children+=KRendering)*)?
    '}')?;

KArc returns KArc:
    {KArc}
    'Arc' startAngle=EFloat ','? arcAngle=EFloat
    ('{'
        ('styles' ':'? styles+=KStyle ( ","? styles+=KStyle)*)?
        ('placementData' ':'? placementData=KPlacementData)?
        ('childPlacement' ':'? childPlacement=KPlacement)?
        ('children' ':'? children+=KRendering ( ","? children+=KRendering)*)?
    '}')?;

KChildArea returns KChildArea:
    {KChildArea}
    'ChildArea'
    ('{'
        ('styles' ':'? styles+=KStyle ( ","? styles+=KStyle)*)?
        ('placementData' ':'? placementData=KPlacementData)?
    '}')?;

KText returns KText:
    {KText}
    'Text' (text=EString)?
    ('{'
        (clip?='clip')?
        ('styles' ':'? styles+=KStyle ( ","? styles+=KStyle)*)?
        ('placementData' ':'? placementData=KPlacementData)?
        ('childPlacement' ':'? childPlacement=KPlacement)?
        ('children' ':'? children+=KRendering ( ","? children+=KRendering)*)?
        // allow mapProperties in order to specify size for size estimation tests
        // (I don't trust in the different SWT implementations to
        // provide the same size of a text on different platforms)
        ('mapProperties' ':'? persistentEntries+=PersistentEntry ( ","? persistentEntries+=PersistentEntry)*)?
    '}')?;

KCustomRendering returns KCustomRendering:
    {KCustomRendering}
    'CustomRendering'
    ('{'
        'className' className=EString
        'bundleName' bundleName=EString
        ('styles' ':'? styles+=KStyle ( ","? styles+=KStyle)*)?
        ('placementData' ':'? placementData=KPlacementData)?
        ('childPlacement' ':'? childPlacement=KPlacement)?
        ('children' ':'? children+=KRendering ( ","? children+=KRendering)*)?
    '}')?;

KSpline returns KSpline:
    {KSpline}
    'Spline'
    ('{'
        ('styles' ':'? styles+=KStyle ( ","? styles+=KStyle)*)?
        ('placementData' ':'? placementData=KPlacementData)?
        ('childPlacement' ':'? childPlacement=KPlacement)?
        ('children' ':'? children+=KRendering ( ","? children+=KRendering)*)?
    '}')?;

KDecoratorPlacementData returns KDecoratorPlacementData:
    'DecoratorPlacementData'
    '{'
        (rotateWithLine?='rotateWithLine')?
        'absolute' absolute=EFloat
        ('relative' relative=EFloat)?
        ('xOffset' xOffset=EFloat)?
        ('yOffset' yOffset=EFloat)?
        ('width' width=EFloat)?
        ('height' height=EFloat)?
    '}';

KGridPlacementData returns KGridPlacementData:
    {KGridPlacementData}
    'GridPlacementData'
    '{'
        ('widthHint' widthHint=EFloat)?
        ('heightHint' heightHint=EFloat)?
        ('topLeft' topLeft=KPosition)?
        ('bottomRight' bottomRight=KPosition)?
    '}';

KDirectPlacementData returns KDirectPlacementData:
    'DirectPlacementData'
    '{'
        'topLeft' topLeft=KPosition ","?
        'bottomRight' bottomRight=KPosition
    '}';

KPosition returns KPosition:
    x=KXPosition '/' y=KYPosition;

KLeftPosition returns KLeftPosition:
    {KLeftPosition}
    'left' absolute=EFloat relative=EFloat;

KRightPosition returns KRightPosition:
    {KRightPosition}
    'right' absolute=EFloat relative=EFloat;

KTopPosition returns KTopPosition:
    {KTopPosition}
    'top' absolute=EFloat relative=EFloat;

KBottomPosition returns KBottomPosition:
    {KBottomPosition}
    'bottom' absolute=EFloat relative=EFloat;

KForegroundColor returns KForegroundColor:
    {KForegroundColor}
    'foregroundColor' red=EInt  green=EInt blue=EInt (propagateToChildren?='!')?;

KBackgroundColor returns KBackgroundColor:
    {KBackgroundColor}
    'backgroundColor' red=EInt green=EInt blue=EInt (propagateToChildren?='!')?;

KLineWidth returns KLineWidth:
    'lineWidth' lineWidth=EInt (propagateToChildren?='!')?;

KAlpha returns KAlpha:
    KForegroundAlpha | KBackgroundAlpha
;

KForegroundAlpha returns KForegroundAlpha:
    {KForegroundAlpha}
    'foregroundAlpha' alpha=EFloat propagateToChildren?='!'?
;


KBackgroundAlpha returns KBackgroundAlpha:
    {KBackgroundAlpha}
    'backgroundAlpha' alpha=EFloat propagateToChildren?='!'?
;

KLineStyle returns KLineStyle:
    {KLineStyle}
    'lineStyle' lineStyle=LineStyle propagateToChildren?='!'?;
    
KRotation returns KRotation:
    {KRotation}
    'rotation' rotation=EFloat propagateToChildren?='!'?;
    
KFontBold returns KFontBold:
    {KFontBold}
    bold?='bold' propagateToChildren?='!'?;
    
KFontItalic returns KFontItalic:
    {KFontItalic}
    italic?='italic' propagateToChildren?='!'?;
    
KFontName returns KFontName:
    'font' name=EString propagateToChildren?='!'?;

KFontSize returns KFontSize:
    'fontSize' size=EInt propagateToChildren?='!'?;

KVerticalAlignment returns KVerticalAlignment:
    {KVerticalAlignment}
    'verticalAlignment' verticalAlignment=VerticalAlignment propagateToChildren?='!'?;
    
KHorizontalAlignment returns KHorizontalAlignment:
    {KHorizontalAlignment}
    'horizontalAlignment' horizontalAlignment=HorizontalAlignment propagateToChildren?='!'?;

//KGradient returns KGradient:
//    {KGradient}
//    'angle' angle=EFloat
//    'startColor' red=EInt green=EInt blue=EInt
//    'endColor' red=EInt green=EInt blue=EInt
//;

KGridPlacement returns KGridPlacement:
    {KGridPlacement}
    'gridPlacement' numColumns=EInt;
    

enum LineStyle returns LineStyle:
    SOLID = 'SOLID' | DASH = 'DASH' | DOT = 'DOT' | DASHDOT = 'DASHDOT' | DASHDOTDOT = 'DASHDOTDOT';

enum VerticalAlignment returns VerticalAlignment:
    TOP = 'TOP' | CENTER = 'CENTER' | BOTTOM = 'BOTTOM';

enum HorizontalAlignment returns HorizontalAlignment:
    LEFT = 'LEFT' | CENTER = 'CENTER' | RIGHT = 'RIGHT';

EFloat returns ecore::EFloat:
    '-'? INT ('.' INT (('E'|'e') '-'? INT)?)?;

EBoolean returns ecore::EBoolean:
    'true' | 'false';

EInt returns ecore::EInt:
    '-'? INT;
